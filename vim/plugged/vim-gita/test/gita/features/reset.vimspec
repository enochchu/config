Describe gita#features#reset
  Before
    Init
    let gita = gita#get()
    let gita.operations._previous_args = []
    function! gita.operations.reset(...) abort
      call add(self._previous_args, a:000)
    endfunction
  End

  Context #exec([{options}, {config}])
    It should call 'gita.operations.reset' internally
      call gita#features#reset#exec({
            \ 'q': 1, 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \ 'N': 1,
            \ 'hard': 1,
            \ 'merge': 1,
            \ 'keep': 1,
            \ 'commit': 1,
            \})
      Assert Equals(gita.operations._previous_args, [[{
            \ 'q': 1, 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \ 'N': 1,
            \ 'hard': 1,
            \ 'merge': 1,
            \ 'keep': 1,
            \ 'commit': 1,
            \}, {}]])
    End

    It should remove unknown options internally
      call gita#features#reset#exec({
            \ 'unknown_option': 1,
            \})
      Assert Equals(gita.operations._previous_args, [[{}, {}]])
    End
  End

  Context #command({bang}, {range}[, {cmdline}])
    It should automatically specify 'quiet = 1' but should not apply %
      let g:gita#features#reset#default_options = {}
      call gita#features#reset#command('', [0, 0], '')
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': [],
            \ 'quiet': 1,
            \}, { 'echo': 'both' }]])
    End
    It should automatically expand '%' to absolute path
      let g:gita#features#reset#default_options = {}
      call gita#features#reset#command('', [0, 0], '-- %')
      " Note: git reset require UNIX path
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': gita#utils#path#unix_abspath(['%']),
            \ 'quiet': 1,
            \}, { 'echo': 'both' }]])
    End
    It should automatically add default options
      let g:gita#features#reset#default_options = {
            \ 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \}
      call gita#features#reset#command('', [0, 0])
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': [],
            \ 'quiet': 1,
            \ 'soft': 1,
            \ 'mixed': 1,
            \}, { 'echo': 'both' }]])
    End
  End

  Context #complete({arglead}, {cmdline}, {cursorpos})
    It should call ArgumentParser.complete function
      Assert True(exists('*gita#features#reset#complete'))
    End
  End
End
