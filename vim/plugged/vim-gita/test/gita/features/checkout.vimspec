Describe gita#features#checkout
  Before
    Init
    let gita = gita#get()
    let gita.operations._previous_args = []
    function! gita.operations.checkout(...) abort
      call add(self._previous_args, a:000)
    endfunction
  End

  Context #exec([{options}, {config}])
    It should call 'gita.operations.checkout' internally
      call gita#features#checkout#exec({
            \ 'q': 1, 'quiet': 1,
            \ 'f': 1, 'force': 1,
            \ 'ours': 1, 'theirs': 1,
            \ 'b': 1, 'B': 1,
            \ 't': 1, 'track': 1, 'no-track': 1,
            \ 'l': 1,
            \ 'detach': 1,
            \ 'orphan': 1,
            \ 'commit': 1,
            \})
      Assert Equals(gita.operations._previous_args, [[{
            \ 'q': 1, 'quiet': 1,
            \ 'f': 1, 'force': 1,
            \ 'ours': 1, 'theirs': 1,
            \ 'b': 1, 'B': 1,
            \ 't': 1, 'track': 1, 'no-track': 1,
            \ 'l': 1,
            \ 'detach': 1,
            \ 'orphan': 1,
            \ 'commit': 1,
            \}, {}]])
    End

    It should remove unknown options internally
      call gita#features#checkout#exec({
            \ 'unknown_option': 1,
            \})
      Assert Equals(gita.operations._previous_args, [[{}, {}]])
    End
  End

  Context #command({bang}, {range}[, {cmdline}])
    It should assume the first unknown argument is 'commit' argument
      let g:gita#features#checkout#default_options = {}
      call gita#features#checkout#command('', [0, 0], 'foo')
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': [],
            \ 'commit': 'foo',
            \}, { 'echo': 'both' }]])
    End
    It should automatically add unknown options to '--'
      let g:gita#features#checkout#default_options = {}
      call gita#features#checkout#command('', [0, 0], 'foo bar hoge')
      " Note: git checkout requires UNIX path
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': [
            \   gita#utils#path#unix_abspath('bar'),
            \   gita#utils#path#unix_abspath('hoge'),
            \ ],
            \ 'commit': 'foo',
            \}, { 'echo': 'both' }]])
    End
    It should automatically add default options
      let g:gita#features#checkout#default_options = {
            \ 'quiet': 1,
            \ 'force': 1,
            \ 'ours': 1,
            \}
      call gita#features#checkout#command('', [0, 0])
      Assert Equals(gita.operations._previous_args, [[{
            \ '--': [],
            \ 'quiet': 1,
            \ 'force': 1,
            \ 'ours': 1,
            \}, { 'echo': 'both' }]])
    End
  End

  Context #complete({arglead}, {cmdline}, {cursorpos})
    It should call ArgumentParser.complete function
      Assert True(exists('*gita#features#checkout#complete'))
    End
  End
End

